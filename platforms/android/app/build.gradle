apply plugin: 'com.android.application'
apply plugin: 'com.taobao.android.weex.plugin.gradle'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.xzl.jicu"
        minSdkVersion project.appMinSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        ndk {
            abiFilters "x86"
            abiFilters "armeabi"
        }
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "322f4fe6d107907c07775867", //JPush 上注册的包名对应的 Appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]
    }


    flavorDimensions "default"

    productFlavors {
        develop { dimension "default" }
        xiaomi { dimension "default" }
        huawei { dimension "default" }
        anzhi { dimension "default" }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            //println "输出apk地址:" + outputFile.parentFile.absolutePath + java.io.File.separator
            def fileName = "jicu_${variant.productFlavors[0].name}-${defaultConfig.versionCode}-${defaultConfig.versionName}-${releaseTime()}.apk"
            println "输出apk的名字:" + outputFileName
            outputFileName = fileName
        }
    }
    signingConfigs {
        release {
            storeFile file("tools/release.jks")
            storePassword "1qaz2wsX"
            keyAlias "wayandroidrelease"
            keyPassword "Xsw2zaq1"
        }
        debug {
            storeFile file("tools/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    sourceSets {
        main {
            java {
                srcDirs = ["src/main/java", "src/main/java_zxing"];
            }
        }
    }
    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.taobao.android:weex_sdk:0.16.0'
    implementation 'com.google.code.findbugs:jsr305:2.0.1'
    implementation 'com.squareup.okhttp:okhttp:2.3.0'
    implementation 'com.squareup.okhttp:okhttp-ws:2.3.0'
    implementation 'com.squareup.okio:okio:1.0.1'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation "com.alibaba:fastjson:${project.fastjsonLibVersion}"
    implementation "com.android.support:design:${project.supportLibVersion}"
    implementation "com.android.support:support-annotations:${project.supportLibVersion}"
    implementation 'com.taobao.android:weex_inspector:0.10.0.5'
    implementation 'com.journeyapps:zxing-android-embedded:3.4.0'
    implementation 'com.amap.api:location:4.4.0'
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:5.1.6'
    implementation 'cn.jiguang.sdk:jpush:3.1.6'  // 此处以JPush 3.1.6 版本为例。
    implementation 'cn.jiguang.sdk:jcore:1.2.5'  // 此处以JCore 1.2.5 版本为例。
    implementation 'com.facebook.fresco:fresco:0.12.0'
    implementation 'com.facebook.fresco:animated-gif:0.12.0'
    // PLUGIN GRADLE EXTENSIONS START
    implementation 'com.taobao.android:weexplugin-loader:1.3'
    implementation 'com.taobao.android:weexplugin-processor:1.3'
    implementation 'com.taobao.android:weexplugin-annotation:1.3'
    // PLUGIN GRADLE EXTENSIONS END
    implementation "com.android.support:support-v4:${project.supportLibVersion}"
    implementation "com.android.support:appcompat-v7:${project.supportLibVersion}"
}
